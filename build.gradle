plugins {
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id 'java'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: "dev.architectury.loom"

    version = project.mod_version
    group = project.maven_group

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    repositories {
        maven {
            name = 'Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            url "https://maven.parchmentmc.net/"
            content {
                includeGroup "org.parchmentmc.data"
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.19.2:2022.10.09@zip")
        }
        modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    }

    var accessWidener = new File(rootProject.projectDir, 'src/main/resources/tree-hollows.accesswidener')
    loom {
        accessWidenerPath = accessWidener
        shareCaches()
    }
}

subprojects {
    archivesBaseName = "tree-hollows-" + project.name
}